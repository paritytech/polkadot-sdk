default: build create-specs launch

# Set default values for the named preset
named-preset := "development"
log := "runtime::multiblock-election=info,runtime::staking=info"
para-id := "1100"
relay-chain := "rococo-local"
parachain-runtime-path := "../../../../../target/release/wbuild/pallet-staking-async-parachain-runtime/pallet_staking_async_parachain_runtime.compact.compressed.wasm"
rc-runtime-path := "../../../../../target/release/wbuild/pallet-staking-async-rc-runtime/fast_runtime_binary.rs.wasm"


build:
  RUST_LOG={{log}} cargo build --release -p pallet-staking-async-rc-runtime -p pallet-staking-async-parachain-runtime -p staging-chain-spec-builder

create-specs:
  rm -f ./parachain.json ./rc.json

  RUST_LOG={{log}} ../../../../../target/release/chain-spec-builder \
    create \
    -t development \
    --runtime {{parachain-runtime-path}} \
    --relay-chain {{relay-chain}} \
    --para-id {{para-id}} \
    named-preset {{named-preset}}
  mv ./chain_spec.json ./parachain.json

  RUST_LOG={{log}} ../../../../../target/release/chain-spec-builder \
    create \
    -t development \
    --runtime {{rc-runtime-path}} \
    named-preset local_testnet
  mv ./chain_spec.json ./rc.json

launch:
  zombienet --provider native -l text spawn zombienet-staking-runtimes.toml

no-compile: create-specs launch
